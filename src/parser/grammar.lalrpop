use ast::{Program, Stmt};
use ast::{BinOpKind, UnOpKind};
use ast::{Expr, ExprKind, Literal};
use datatype::{DataType, Field};
use parser::{lexer, location, token};

grammar;

Box<T>: Box<T> = T => Box::new(<>);

Expr<T>: Expr<T> = T => Expr::new(<>);
ExprBox<T> = Box<Expr<T>>;

Binary<Op, Next>: Expr = {
    <lhs:Box<Binary<Op, Next>>> <o:Op> <rhs:Box<Next>>
        => Expr::Binary(o, lhs, rhs),
    Next
};

List<T>: Vec<T> = <a:(<T> ",")*> <b:T>
    => {
        let mut a = a;
        a.push(b);
        a
    };

OptList<T>: Vec<T> = List<T>?
    => <>.unwrap_or(Vec::new());

pub Program: Program = Stmt*
    => Program(<>);

Stmt: Stmt = {
    CompoundStmt,
    IfStmt,
    WhileStmt,
    FnDeclStmt,
    StructDeclStmt,
    <ShortFnDeclStmt> ";",
    <ReturnStmt> ";",
    <Box<Expression>> ";"
        => Stmt::Expr(<>)
};

CompoundStmt: Stmt = "{" <Stmt*> "}"
    => Stmt::Compound(<>);

WhileStmt: Stmt = "while" <cond:Box<Expression>> <csq:Box<CompoundStmt>>
    => Stmt::While(cond, csq);

IfStmt: Stmt = "if" <cond:Box<Expression>> <csq:Box<CompoundStmt>> <alt:("else" <Box<IfOrBlock>>)?>
    => Stmt::If(cond, csq, alt);

IfOrBlock = {
    CompoundStmt,
    IfStmt
};

ReturnStmt: Stmt = "return" <Box<Expression>>
    => Stmt::Return(<>);

FnDeclStmt: Stmt = "func" <name:Ident> "(" <params:OptList<FieldList>> ")" <rt:("->" <DataType>)?> <body:Box<CompoundStmt>>
    => {
        let params = params.into_iter().flat_map(|x| x.into_iter()).collect();
        Stmt::FuncDecl(name, rt.unwrap_or(DataType::Void), params, body)
    };

ShortFnDeclStmt: Stmt = "func" <name:Ident> "(" <params:OptList<FieldList>> ")" <rt:("->" <DataType>)?> "=" <body:Box<Expression>>
    => {
        let params = params.into_iter().flat_map(|x| x.into_iter()).collect();
        Stmt::FuncDecl(name, rt.unwrap_or(DataType::Void), params, Box::new(Stmt::Return(body)))
    };

Field: Field = <i:Ident> ":" <t:DataType>
    => Field(t, i);

FieldList: Vec<Field> = <is:List<Ident>> ":" <t:DataType>
    => is.into_iter().map(|i| Field(t.clone(), i)).collect();

StructDeclStmt: Stmt = "struct" <name:Ident> "{" <fields:(<FieldList> ";")+> "}"
    => Stmt::StructDecl(name, fields.into_iter().flat_map(|x| x.into_iter()).collect());

Expression = Declaration;

Declaration: Expr = {
    <field:Field> <val:("=" <Box<Declaration>>)?>
        => Expr::Decl(field, val),
    Assignment
};

Assignment = Binary<AssignmentKind, Cast>;
AssignmentKind: BinOpKind = "="
    => BinOpKind::Assign;

Cast = {
    <lhs:Box<Cast>> "as" <dt:DataType>
        => Expr::Cast(lhs, dt),
    Sum
};

Sum = Binary<SumKind, Factor>;
SumKind: BinOpKind = {
    "+" => BinOpKind::Add,
    "-" => BinOpKind::Sub
};

Factor = Binary<FactorKind, Unary>;
FactorKind: BinOpKind = {
    "*" => BinOpKind::Mul,
    "/" => BinOpKind::Div,
    "%" => BinOpKind::Mod,
};

Unary = {
    <o:UnaryKind> <rhs:Box<Unary>>
        => Expr::Unary(o, rhs),
    PostfixExpr
};

UnaryKind: UnOpKind = {
    "-" => UnOpKind::Neg,
    "~" => UnOpKind::Compl,
    "!" => UnOpKind::Not,
    "*" => UnOpKind::Deref,
    "&" => UnOpKind::Ref
};

PostfixExpr: Expr = {
    <lhs:Box<PostfixExpr>> "(" <params:OptList<Expression>> ")"
        => Expr::Call(lhs, params),
    <lhs:Box<PostfixExpr>> "[" <rhs:Box<Expression>> "]"
        => Expr::Subscript(lhs, rhs),
    Atom
};

Atom: Expr = {
    Literal => Expr::Literal(<>),
    "(" <Expression> ")",
    Ident => Expr::Name(<>)
};

Literal: Literal = {
    Integer => Literal::Integer(<>)
};

DataType: DataType = {
    "u8" => DataType::U8,
    "void" => DataType::Void,
    <Box<DataType>> "*" => DataType::Ptr(<>),
    Ident => DataType::Alias(<>)
};

Integer: i64 = "INTEGER"
    => match <> {
        token::Token::Integer(s) => s.parse::<i64>().unwrap(),
        _ => unreachable!()
    };

Ident: String = "IDENT"
    => match <> {
        token::Token::Ident(s) => s,
        _ => unreachable!()
    };


extern {
    type Location = location::Location;
    type Error = lexer::UnexpectedCharError;

    enum token::Token {
        "," => token::Token::Comma,
        ";" => token::Token::Semi,
        "{" => token::Token::BraceOpen,
        "}" => token::Token::BraceClose,
        "(" => token::Token::ParenOpen,
        ")" => token::Token::ParenClose,
        "[" => token::Token::BracketOpen,
        "]" => token::Token::BracketClose,
        "=" => token::Token::Equals,
        ":" => token::Token::Colon,
        "+" => token::Token::Plus,
        "-" => token::Token::Minus,
        "*" => token::Token::Star,
        "/" => token::Token::Slash,
        "%" => token::Token::Percent,
        "~" => token::Token::Tilde,
        "!" => token::Token::Exclamation,
        "&" => token::Token::Ampersand,
        "->" => token::Token::Arrow,
        "while" => token::Token::While,
        "if" => token::Token::If,
        "else" => token::Token::Else,
        "return" => token::Token::Return,
        "func" => token::Token::Func,
        "struct" => token::Token::Struct,
        "as" => token::Token::As,
        "u8" => token::Token::U8,
        "void" => token::Token::Void,
        "INTEGER" => token::Token::Integer(_),
        "IDENT" => token::Token::Ident(_),
    }
}